// VERB PHRASES
// ================================================
 
// declarative sentence

=[%fact]
  <np> (<adv3>) <vp> ((<np>) <np>) (<vmod3>)
  <np> <vp> (<np>) to <cmd>
  <np> <vp> (<np>) <%fact-g>
  <%obj-i0> <$add>
//  <%obj-i> <$add>
  <%obj> <$add>
  there is <%obj-i>


// fact about some indeterminate object

=[%fact-i]
  <%obj-i> <$add>


// abstract action by some agent or participial phrase

=[%fact-g]
  (<BLF>) (<adv3>) <ACT-G> (<np> (<np>)) (<vmod3>)


// inverted fact used as a noun modifier

=[%fact-m]
  (<ref-t>) <np> <vp>


// assigning names ("is" optional for yn question)

=[%fact-n]
  <NAME> (is) a name
  <NAME> (is) (<art>) <desc-p> name
  (<art>) <desc-p> name (is) <NAME>


// verb phrase

=[vp]
  (<BLF>) <ACT-S>
  (<BLF>) <ACT-D>
  (<BLF>) (<AUX-D>) (<BLF>) (<NEG>) <ACT>
  (<BLF>) <aux> (<BLF>) (<NEG>) <ACT-G>


// ==================================================

// action to take

=[cmd]
  <!ach> (<too>)
  <!do> (<too>)
  <!find> (<too>)
  <!find-t> (<too>)
  <!find-c> (<too>)
  <!chk> (<too>)
  <!chk-t> (<too>)
  <!for> (<too>)


// hypothetical action (not a command)

=[cmd-s]
  <!do-s>
  <!find-s>
  <!chk-s>


// explicit multi-step loop

=[!for]
  for <%obj>


// partial repeat indicator

=[too]
  too
  also


// --------------------------------------------------

// ACH directive

=[!ach]
  make sure (that) <%fact>


// DO directive ("you" or missing subject, 0 to 2 objects)

=[!do]
  (you <PREF>) do nothing
  (you <PREF>) (<adv3>) <ACT> (<np> (<np>)) (<vmod3>)
  (you <PREF>) (<adv3>) <ACT-2> <np> <$dest> (<manner>)
  (you <PREF>) (<adv3>) <ACT-2> <np> <$rel-d> (<manner>)
  (you <PREF>) (<adv3>) <ACT-2> <np> <DIR> (<manner>)
  (you <PREF>) (<adv3>) <ACT-2> <DIR> <np> (<manner>)
  (you <PREF>) (<adv3>) <SAY> (to <np>) <QUOTE>
  <STOP> <ACT-G> (<np> (<np>)) (<vmod3>)
  <NEG-V> <ACT> (<np> (<np>)) (<vmod3>)
//  <ACT> (that) <%fact>                         // "know"?
  

// hypothetical DO directive for prohibition

=[!do-s]
  (<adv3>) <ACT> <np-s> (<np-s>) (<vmod3>)
  (<adv3>) <ACT> <np-s> <np> (<vmod3>)
  (<adv3>) <SAY> to <np-s> <QUOTE>


// DO directive phrased as a progressive

=[!do-g]
  <ACT-G> (<np> (<np>)) (<vmod3>)


// request to echo something

=[SAY]
  say
  ask


// from 1 to 16 words

=[QUOTE]
 # (# (# (# (# (# (# (# (# (# (# (# (# (# (# (#)))))))))))))))


// negated verb

=[NEG-V]
  don't 
  do not


// stop some currently on-going action

=[STOP]
  stop
  quit


// --------------------------------------------------

// FIND directive

=[!find]
  find (out) <query>
  find <np>
  look for <np>
  determine <np>
  check <np>


// implicit tell for answering wh-question

=[!find-t]
  <query>


// existence query like a CHK

=[!find-c]
  <be> there <np>


// types of questions

=[query]
  <$q-ako>
  <$q-hq>
  <$q-desc>
  <$q-name>
  <$q-loc>
  <$q-src>
  <$q-cnt>
  <$q-has>
  <$q-mod>


// hypothetical FIND for prohibitions (not used?)

=[!find-s]
  <find-v> <$q-ako-s>
  <find-v> <$q-hq-s>


// find verb phrase

=[find-v]
  find (out)
  check


// embedded question about type

=[$q-ako]
  what (<be>) <np> (<be>)
  what <%n-super> (<be>) <np> (<be>)


// hypothetical embedded question about type

=[$q-ako-s]
  what <np-s> are
  what <n-super> <np-s> are


// embedded question about property

=[$q-hq]
  what <AKO> (<be>) <np> (<be>)
  what (is) the <AKO> <%obj-pp> (is)
//  what (is) <%obj-p> <AKO> (is)
  how <HQ> (<be>) <np> (<be>)
  how (<be>) <np> (<be>)


// hypothetical embedded question about property

=[$q-hq-s]
  what <AKO> <np-s> are


// question about the description of an item

=[$q-desc]
  who (<be>) <np> (<be>)
  who <$add>
//  what <$add>
  what (<be>) <np> (<be>) <ACT-G>


// question about the reference word for an item

=[$q-name]
  what (<be>) <desc-p> name (<be>)
  what (<be>) the name of <np> (<be>)


// question about current location of an item

=[$q-loc]
  where (<be>) <np> (<be>)


// question about starting location of an item
 
=[$q-src]
  where (<be>) <np> (<be>) from


// question about number of items

=[$q-cnt]
  how many <np-s> (are) there (are)
  how many <np-s> (do) you see
  how many <np-s>


// question about ownership

=[$q-has]
  who has <np-any>
  what (<%obj-q>) has <np-any>
  which <%obj-q> has <np-any>


// question about manner of action

=[$q-mod]
  how <aux> <%fact>


// --------------------------------------------------

// CHK directive

=[!chk]
  <check-v> <%fact>
  <check-v> <%fact-n>


// check verb phrase

=[check-v]
  check (if) 
  check that
  find (out) if


// implicit tell for answering yn-question

=[!chk-t]
  is <%fact-n>
  <aux> <%fact>


// ==================================================

// set of adverbs that appear before verb

=[adv3]
  <manner> (<manner> (<manner>))


// set of modifiers that appear after verb

=[vmod3]
  <vmod> (<vmod> (<vmod>))


// generic verb phrase modifier

=[vmod]
  (<DEG>) <AMT>
  <quant>
  <dir3>
  <manner>
  <$rel>
  <$src>
  <$dest>


// explicit numeric quantity

=[quant]
  (about) <^INT> <n>
  for <^INT> <n>
  up to <^INT> <n>


// possibly compound direction

=[dir3]
  (<DEG>) <DIR> (((<DEG>) <DIR>) and (<DEG>) <DIR>)


// manner of acting

=[manner]
  (<DEG>) <MOD> 


// verb adjunct (prepositional phrase) 

=[$rel]
  <place>
  with <np>


// location for ACT-2 verb

=[$rel-d]
  <place>


// action starting place

=[$src]
  from <np>
  from <place>


// action ending place

=[$dest]
  to <np>
  to <place>
  <LOC-D> <np>
//  <place>

