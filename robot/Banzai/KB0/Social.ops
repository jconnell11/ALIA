// basic interface to ELI robot's person finder
// ================================================

OP 1 - "To wander ..."
  trig:   
    DO[ fcn-1 -lex-  wander 
              -fcn-> act-1 ]
---------------
   GND[ call-1 -lex- soc_explore ]


OP 2 - "To make sure I am visible ..."
  trig:   
   ACH[   hq-1 -lex-  visible
               -hq--> user-1
        user-1 -lex-  you ]
---------------
   GND[ call-1 -lex- soc_talk ]


// --------------------------------------

OP 3 - "To look toward some person ..."
  trig:
    DO[ fcn-1 -lex-  look
              -fcn-> act-1
        loc-1 -lex-  toward
              -loc-> act-1
              -ref-> dude-1 
        ako-1 -lex-  person 
              -ako-> dude-1 ]
---------------
   GND[ call-1 -lex-  soc_orient
               -arg-> dude-1 ]


OP 4 - "To look at a person ..."
  trig:   
    DO[ fcn-1 -lex-  look
              -fcn-> act-1
        loc-1 -lex-  at
              -loc-> act-1 
              -ref-> dude-1 
        ako-1 -lex-  person 
              -ako-> dude-1 ]
---------------
   GND[ call-1 -lex-  soc_look
               -arg-> dude-1 ]


OP 5 - "To orient toward a person ..."
  trig:   
    DO[ fcn-1 -lex-  orient
              -fcn-> act-1
        loc-1 -lex-  toward
              -loc-> act-1
              -ref-> dude-1 
        ako-1 -lex-  person 
              -ako-> dude-1 ]
---------------
   GND[ call-1 -lex-  soc_align
               -arg-> dude-1 ]


OP 6 - "To approach a person ..."
  trig:   
    DO[ fcn-1 -lex-  approach
              -fcn-> act-1
        act-1 -obj-> dude-1 
        ako-1 -lex-  person 
              -ako-> dude-1 ]
---------------
   GND[ call-1 -lex-  soc_approach
               -arg-> dude-1 ]


OP 7 - "To follow a person ..."
  trig:   
    DO[ fcn-1 -lex-  follow
              -fcn-> act-1
        act-1 -obj-> dude-1 
        ako-1 -lex-  person 
              -ako-> dude-1 ]
---------------
   GND[ call-1 -lex-  soc_follow
               -arg-> dude-1 ]


// ======================================

OP 8 - "To find a person visually search downwards"
  trig: 
  FIND[ agt-1
        ako-1 -lex-  person
              -ako-> agt-1 ]
---------------
    DO[ fcn-1 -lex-  search
              -fcn-> act-1
        mod-1 -lex-  visually
              -mod-> act-1
        dir-1 -lex-  downwards
              -dir-> act-1 ]


OP 9 - "To come here make sure I am visible then approach me"
  trig:   
    DO[ fcn-1 -lex-  come here 
              -fcn-> act-1 ]
---------------
  FIND[ agt-1 -lex-  you ]
   ACH[  hq-1 -lex-  visible
              -hq--> agt-1 ]
    DO[ fcn-2 -lex-  approach
              -fcn-> act-2
        act-2 -obj-> agt-1 ]


OP 10 - "Before orienting toward me make sure I am visible"
  trig:
  ANTE[  fcn-1 -lex-  orient
               -fcn-> act-1
         loc-1 -lex-  toward
               -loc-> act-1
               -ref-> user-1 
        user-1 -lex-  you ]
---------------
   ACH[  hq-1 -lex-  visible
              -hq--> user-1 ]

 