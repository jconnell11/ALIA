// Basic interface to "Support" kernel
// ================================================

OP 1 - "To check if some object is on some surface ..."
  trig:
   CHK[ loc-1 -lex-  on
              -loc-> obj-1
              -ref-> surf-1
        ako-1 -lex-  object
              -ako-> obj-1
        ako-2 -lex-  surface
              -ako-> surf-1 ]
----------
   GND[ call-1 -lex-   surf_on_ok 
               -arg--> obj-1 
               -arg2-> surf-1 ] 
    

OP 2 - "To look at some surface ..."
  trig:
    DO[ fcn-1 -lex-  look
              -fcn-> act-1
        loc-1 -lex-  at
              -loc-> act-1
              -ref-> surf-1 
        ako-1 -lex-  surface
              -ako-> surf-1 ]
----------
   GND[ call-1 -lex-  surf_look
               -arg-> surf-1 ] 


OP 3 - "To look toward some surface ..."
  trig:
    DO[ fcn-1 -lex-  look
              -fcn-> act-1
        loc-1 -lex-  toward
              -loc-> act-1
              -ref-> surf-1 
        ako-1 -lex-  surface
              -ako-> surf-1 ]
----------
   GND[ call-1 -lex-  surf_orient
               -arg-> surf-1 ] 


OP 4 - "To approach some surface ..."
  trig:
    DO[ fcn-1 -lex-  approach
              -fcn-> act-1
        act-1 -obj-> surf-1
        ako-1 -lex-  surface
              -ako-> surf-1 ]
----------
   GND[ call-1 -lex-  surf_goto
               -arg-> surf-1 ] 


// ======================================

OP 5 - "To find some surface ..."
  trig:
  FIND[ obj-1 
        ako-1 -lex-  surface
              -ako-> obj-1 ]
----------
   GND[ call-1 -lex-  surf_enum 
               -arg-> obj-1 ] 


OP 6 - "To find some surface search visually upwards"
  trig:
  FIND[ obj-1 
        ako-1 -lex-  surface
              -ako-> obj-1 ]
----------
    DO[ fcn-1 -lex-  search
              -fcn-> act-1
        mod-1 -lex-  visually
              -mod-> act-1
        dir-1 -lex-  upwards
              -dir-> act-1 ]


OP 7 - "To find an object on a surface make sure the surface is close"
  trig:
  FIND[ obj-1
        ako-1 -lex-  object
              -ako-> obj-1
        loc-1 -lex-  on
              -loc-> obj-1
              -ref-> surf-1
        ako-2 -lex-  surface
              -ako-> surf-1 ]
----------
   ACH[ hq-1 -lex-  close
             -hq--> surf-1 ]


OP 8 - "To make sure some surface is close approach it" 
  trig:
   ACH[  hq-1 -lex-  close
              -hq--> surf-1
        ako-1 -lex-  surface
              -ako-> surf-1 ]
----------
    DO[ fcn-1 -lex-  approach
              -fcn-> act-1
        act-1 -obj-> surf-1 ]
