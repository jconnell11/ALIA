// NOUN PHRASES
// ================================================

// noun phrase (but not plural indefinite)

=[np]
  <%obj-i0>
  <%obj-i>
  <%obj>
  <query>


// noun phrase including plural indefinites

=[np-s]
  <%obj-s>


// possessive noun phrase

=[np-poss]
  [WRT]


// vague indefinite noun phrase ("something red on the table")

=[%obj-i0]
  <n-ind> (<desc>) (<comp>)


// indefinite noun phrase

=[%obj-i]
  <ind> (<desc>) <AKO> (<comp>)
  <ind> (<desc>) thing (<comp>)
  <AKO-M>


// indefinite plural noun phrase

=[%obj-s]
  (<desc>) <AKO-S> ((<be>) <comp>)
  (<desc>) things ((<be>) <comp>)


// definite noun phrase

=[%obj]
  <def> (<desc>) <n> (<comp>)
  <WRT> (<desc>) <n> (<comp>)
  (<desc>) <NAME> (<comp>)
  <REF-D>
  <REF>
  <REF-L>


// noun

=[n]
  thing
  <AKO>
  <AKO-S>


// superclass of some noun type

=[%n-super]
  kind of <AKO>
  type of <AKO>


// --------------------------------------------------

// description (up to 3 adjectives)

=[desc]
  <nmod> ((and) <nmod> ((and) <nmod>))


// one modifiying term

=[nmod]
  (<NEG>) (<DEG>) <HQ>
  <HQ-EST>
  <ACT-G>
  <AKO>


// complementing term (usually PP)

=[comp]
  <HAS> <np>
  that <be> <HQ>
  <place>
  <HQ-ER> than <%obj>
 

// types of location phrases

=[place]
  (<DEG>) <LOC> <np>
  <LOC-2> <np> and <np>
  <LOC-V>


// --------------------------------------------------

// copula verb phrase ("be" optional for yn-questions)

=[$add]
  (<cop>) (<NEG>) <NAME>
  (<cop>) <desc> 
  (<cop>) <comp>
  (<cop>) <%obj-i>
  (<cop>) <%obj-s>
  (<cop>) a <%n-super>


// copula core
 
=[cop]
  <be> (<BLF>) 
  <BLF> <be>




