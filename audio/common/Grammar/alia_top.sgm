// TOP LEVEL PATTERNS
// ================================================

#include "alia_NP.sgm"
#include "alia_VP.sgm"
#include "alia_lex.sgm"
#include "integer.sgm"


// valid utterances once attention established

=[toplevel]
  (hey (there)) <ATTN>
  ((hey) <ATTN>) <HELLO> (<$intro>) (<ATTN>)
  ((hey) <ATTN>) <$intro> (<ATTN>)
  ((hey) <ATTN>) <BYE> (<ATTN>)
  <kudo> (<ATTN>)
  ((hey) <ATTN>) <main> (<ATTN>)


// introduction of user by name

=[$intro]
  I am (<NEG>) <NAME>
  this is (<NEG>) <NAME>
  my name is (<NEG>) <NAME>


// main categories of utterances

=[main]
  <%Immediate>
  <%Revision>
  <%Rule>
  <%Operator>


// --------------------------------------------------

// opening feedback remarks ("yup" and "bad boy")

=[kudo]
  <YES>
  <NO>
  <ok>
  <HQ> (<AKO>)
  <AKO>


// affirmative opening

=[YES]
  yes
  yup
  yeah
  right


// critical opening 

=[NO]
  no
  nope
  wrong


// neutral positive opening

=[ok]
  okay
  sure


// --------------------------------------------------

// something to do or note

=[%Immediate]
  (<POLITE>) <chain> (<POLITE>)
  (<YES>) <%fact>
  (<NO>) <%fact>
  (<YES>) <%fact-n>
  (<NO>) <%fact-n>


// --------------------------------------------------

// make a change to some procedural operator

=[%Revision]
  <ACC> <cmd>                                         
  <REJ> <NEG-V> <cmd>                     
  (<ACC>) or (instead) (you <PREF>) <$proc> (instead)                         
  (<REJ>) (you <PREF>) <$proc> instead                    
  (<REJ>) instead (you <PREF>) <$proc> 
  (<REJ>) <NEG-V> <cmd> but <$proc>
  (<REJ>) <NEG-V> <cmd> (but) <$proc> instead
  (<REJ>) <NEG-V> <cmd> (but) instead <$proc>                       
  (<ACC>) (or) you <PREF> <$proc> instead of <cmd-g>   
  (<ACC>) instead of <cmd-g> you <PREF> <$proc>


// acceptance of prior action

=[ACC]
  yes
  yeah
  yup
  okay
  sure
  good


// rejection of prior action

=[REJ]
  no
  nope


// --------------------------------------------------

// halo deductive rule

=[%Rule]
  <$cond> (<then>) <$res>
  <$res> <$cond>
  <$macro>
  <$cond-s> <$res-s>
  <$cond-i> <$res-i>


// rule precondition

=[$cond]
  <situation>


// rule result

=[$res]
  <%fact>
  <%fact-n>


// rule precondition (indefinite)

=[$cond-i]
  <%obj-i>
  <HQ>
  <MOD>


// rule result (for indefinite)

=[$res-i]
  <$add>


// rule precondition (plural indefinite)

=[$cond-s]
  <%obj-s>


// rule result (for plural indefinite)

=[$res-s]
  <vp> ((<np-s>) <np-s>)
  <vp> (<np-s>) to <cmd>
  <$add>


// term redefinition (for modifiers of various sorts)

=[$macro]
  (<DEG>) <HQ> <equiv> (<DEG>) <HQ>
  <DIR> <equiv> <DIR>
  <MOD> <equiv> <MOD>
  <ACT-G> <equiv> <ACT-G>


// predicate equivalence relation 

=[equiv]
  means
  implies


// --------------------------------------------------

// procedural memory operator

=[%Operator]
  (<so>) <$trig> (<then>) <$proc> (<$trig>)
  (<so>) <$trig> <equiv> <$proc> (<$trig>)
  (<so>) <$proc> <$trig>
  (<so>) <$trig-n> ((<conj>) instead <$proc> (<$trig>))
  (<so>) <$trig-p>
 

// operator trigger goal

=[$trig]
  <inf> <cmd> (<situation>)
  <inf> <cmd> <BEFORE>
  <if> <FAIL> <!find>
  <BEFORE> <cmd-g> (<situation>) 
  <AFTER> <cmd-g> (<situation>)
  <situation> then <BEFORE> <cmd-g> 
  <situation> then <AFTER> <cmd-g>
  <situation> then <inf> <cmd>
  <situation>


// phrase indicating some command did not work

=[FAIL]
  you cannot
  you can't
  you could not
  you couldn't
  you did not
  you didn't
  you do not
  you don't


// contemplated action

=[cmd-g]
  you (start to) <cmd>
  you begin to <cmd>
  starting to <cmd>
  you start <!do-g>
  (you stop) <!do-g>
  you finish <!do-g>


// advice not to do something

=[$trig-n]
  <dont> (ever) <cmd> (<situation>)
  (you should) never <cmd> (<situation>)
  <dont> <cmd-s> (<situation>)
  (you should) never <cmd-s> (<situation>)
  <situation> <dont> <cmd>
  <situation> <dont> <cmd-s>
  <situation> (<then>) <punt>
  <punt> <situation>


// prohibition phrases

=[dont]
  do not
  don't
  you should not
  you shouldn't 


// probibition actions

=[punt]
  do not (comply)
  don't (comply)
  refuse (to (<ref-v>))
  do not <ref-v>
  don't <ref-v>
  do nothing


// reference to an action

=[ref-v]
  do it
  do that


// advice to override a prohibition

=[$trig-p]
  <allow> <cmd> (<situation>)
  <situation> (<then>) <allow> <cmd>


// permission to do something

=[allow]
 you can
 it is okay to
 it's okay to
 it is ok to
 it's ok to


// operator procedure to follow (PREF in do!)

=[$proc]
  <chain>


// ==================================================

// a conjunction of aspects (up to 5 conditions/caveats)

=[situation]
  (<UNLESS> <asp>) <if> <asp>
  (<UNLESS> <asp>) <if> <asp> ((<plus>) <asp> ((<plus>) <asp> ((<plus>) <asp>))) <plus> <asp>
  <UNLESS> <asp>


// an aspect of a situation

=[asp]
  <%fact>
  <%fact-i>
  <%fact-n>
 

// conjunction specification for situation

=[plus]
  <conj> (<if>)
  <UNLESS>


// --------------------------------------------------

// sequence of activites (up to 6 actions)

=[chain]
  <step>
  <!for> <cmd>
  <step> (<step> (<step> (<step> (<step>)))) <seq> <cmd>
  <step> (<step> (<step> (<step> (<step>)))) <seq> <%play>
  <step> (<step> (<step> (<step> (<step>)))) <seq> <%play-t>


// sequential step in chain

=[step]
  <cmd> (<seq>)
  <%play> (<seq>)
  <%play-t> (<seq>)


// sequential continuation

=[seq]
  then
  and (then)


// parallel set of activities (up to 4 simultaneous)

=[%play]
  <cmd> <par-i> <cmd> (((<par>) <cmd>) <par> <cmd>)


// parallel inititiation

=[par-i]
  and also
  and simultaneously
  and at the same time
  as you


// parallel addition
 
=[par]
  and 
  <par-i> 


// activity terminated by some specific condition

=[%play-t]
  <STAY> <%fact> <chain-c>
  <chain-c> <STAY> <%fact>
  <STAY> <%fact> <KEEP> <chain-g> 
  <KEEP> <chain-g> <STAY> <%fact>


// wait for absence or presense of some condition

=[STAY]
  while
  until


// indication that activities should loop

=[KEEP]
  keep
  continue


// series of progressive tense commands

=[chain-g]
  <cmd-g> 
  <cmd-g> ((<seq>) <cmd-g> ((<seq>) <cmd-g>)) <seq> <cmd-g>


//= series of simple commands (no plays)

=[chain-c]
  <cmd>
  <cmd> ((<seq>) <cmd> ((<seq>) <cmd>)) <seq> <cmd>

