// TOP LEVEL PATTERNS
// ================================================

#include "alia_NP.sgm"
#include "alia_VP.sgm"
#include "alia_lex.sgm"


// valid utterances once attention established

=[toplevel]
 (hey) <ATTN>
 ((hey) <ATTN>) <main>
 <main> <ATTN>


// main categories of utterances

=[main]
  <HELLO>
  <BYE>
  <%Attn>
  <%Rule>
  <%Operator>


// --------------------------------------------------

// something to do or note

=[%Attn]
  (<POLITE>) <chain> (<POLITE>)
  <%fact>


// --------------------------------------------------

// halo deductive rule

=[%Rule]
  <$cond> (<then>) <$res>
  <$res> <$cond>
  <$macro>
  <$cond-s> <$res-s>
//  <$cond-i> <$res-i>


// rule precondition

=[$cond]
  <situation>


// rule result

=[$res]
  <%fact>


// rule precondition (indefinite)

=[$cond-i]
  <%obj-i>
  <HQ>


// rule result (for indefinite)

=[$res-i]
  <$add>


// rule precondition (plural indefinite)

=[$cond-s]
  <%obj-s>


// rule result (for plural indefinite)

=[$res-s]
  <vp> ((<np-s>) <np-s>)
  <vp> (<np-s>) to <cmd>
  <$add>


// term redefinition (for modifiers of various sorts)

=[$macro]
  (<DEG>) <HQ> <equiv> (<DEG>) <HQ>
  <DIR> <equiv> <DIR>
  <MOD> <equiv> <MOD>


// modifier equivalence relation 

=[equiv]
  means


// --------------------------------------------------

// procedural memory operator

=[%Operator]
  (<so>) <$trig> (<then>) <$proc> (<$trig>)
  (<so>) <$trig> <equiv> <$proc> (<$trig>)
  (<so>) <$proc> <$trig>
  (<so>) <$trig-n> ((<conj>) instead <$proc> (<$trig>))
 

// operator trigger goal

=[$trig]
  (<inf>) <cmd> (<situation>)
  <inf> <cmd> <BEFORE>
  <if> <FAIL> <!find>
  <BEFORE> <cmd-g> (<situation>) 
  <AFTER> <cmd-g> (<situation>)
  <situation> then <BEFORE> <cmd-g> 
  <situation> then <AFTER> <cmd-g>
  <situation> then <inf> <cmd>
  <situation>


// phrase indicating some command did not work

=[FAIL]
  you cannot
  you can't
  you could not
  you couldn't
  you did not
  you didn't
  you do not
  you don't


// contemplated action

=[cmd-g]
  you (start to) <cmd>
  you begin to <cmd>
  starting to <cmd>
  you start <!do-g>
  (you stop) <!do-g>
  you finish <!do-g>


// advice not to do something

=[$trig-n]
  <dont> ever <cmd> (<situation>)
  (you should) never <cmd> (<situation>)
  <dont> <cmd-s> (<situation>)
  (you should) never <cmd-s> (<situation>)
  <situation> <dont> <cmd>
  <situation> <dont> <cmd-s>
  <situation> (<then>) <punt>
  <punt> <situation>


// prohibition phrases

=[dont]
  do not
  don't
  you should not
  you shouldn't 


// probibition actions

=[punt]
  do not (comply)
  don't (comply)
  refuse (to (<ref-v>))
  do not <ref-v>
  don't <ref-v>
  do nothing


// reference to an action

=[ref-v]
  do it
  do that


// operator procedure to follow (PREF in do!)

=[$proc]
  <chain>


// --------------------------------------------------

// a conjunction of aspects (up to 5 conditions/caveats)

=[situation]
  (<UNLESS> <asp>) <if> <asp>
  (<UNLESS> <asp>) <if> <asp> ((<plus>) <asp> ((<plus>) <asp> ((<plus>) <asp>))) <plus> <asp>
  <UNLESS> <asp>


// an aspect of a situation

=[asp]
  <%fact>
  <%fact-i>
 

// conjunction specification for situation

=[plus]
  <conj> (<if>)
  <UNLESS>


// --------------------------------------------------

// sequence of activites (up to 6 actions)

=[chain]
  <step>
  <step> (<step> (<step> (<step> (<step>)))) <seq> <cmd>
  <step> (<step> (<step> (<step> (<step>)))) <seq> <%play>


// sequential step in chain

=[step]
  <%play> (then)
  <cmd> (<seq>)


// sequential continuation

=[seq]
  then
  and (then)


// parallel set of activities (up to 4 simultaneous)

=[%play]
  <cmd> <par-i> <cmd> (((<par>) <cmd>) <par> <cmd>)


// parallel inititiation

=[par-i]
  and also
  and simultaneously
  and at the same time
  as you


// parallel addition
 
=[par]
  and 
  <par-i> 







